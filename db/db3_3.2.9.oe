DEPENDS = "virtual/libc"
RDEPENDS = "libc6"
DESCRIPTION = "Berkeley DB v3."

SRC_URI = "http://www.sleepycat.com/update/snapshot/db-${PV}.tar.gz"
S = "${WORKDIR}/db-${PV}/dist"
B = "${WORKDIR}/db-${PV}/build_unix"

inherit autotools libtool

EXTRA_OECONF = "--enable-shared --enable-compat185 --enable-static"
EXTRA_OEMAKE = "'SHELL=/bin/sh' 'ar=`which ${AR}` cr' 'chmod=`which chmod`' \
		'cp=`which cp`' 'ln=`which ln`' 'mkdir=`which mkdir`' 'ranlib=`which ${RANLIB}`' \
		'rm=`which rm`' 'strip=`which ${STRIP}`'"

PACKAGES = "${PN} ${PN}-bin ${PN}-dev ${PN}-doc ${PN}-locale"

FILES_${PN} = "${libdir}/libdb-3.2*so*"
FILES_${PN}-bin = "${bindir}"
FILES_${PN}-dev = "${includedir} ${libdir}/libdb.so ${libdir}/libdb.a ${libdir}/libdb-3.so ${libdir}/libdb.la"

do_configure_prepend () {
	set -e
	(
		cd ${S}
		. ./RELEASE
		(echo "AC_DEFUN(AM_VERSION_SET, [" &&
		echo "AC_SUBST(DB_VERSION_MAJOR)" &&
		echo "DB_VERSION_MAJOR=$DB_VERSION_MAJOR" &&
		echo "AC_SUBST(DB_VERSION_MINOR)" &&
		echo "DB_VERSION_MINOR=$DB_VERSION_MINOR" &&
		echo "AC_SUBST(DB_VERSION_PATCH)" &&
		echo "DB_VERSION_PATCH=$DB_VERSION_PATCH" &&
		echo "AC_SUBST(DB_VERSION_STRING)" &&
		echo "DB_VERSION_STRING=\"\\\"$DB_VERSION_STRING\\\"\"" &&
		echo "])dnl") > acinclude.m4
	)
}

do_configure () {
	set -e
	cd ${B}
	oe_runconf --enable-shared --disable-static
	mkdir -p ${WORKDIR}/db-${PV}/build_unix_static
	cd ${WORKDIR}/db-${PV}/build_unix_static
	oe_runconf --disable-shared --enable-static
	cd ${S}
}

do_compile () {
	set -e
	cd ${B}
	oe_runmake
	cd ${WORKDIR}/db-${PV}/build_unix_static
	oe_runmake
	cd ${S}
}

do_stage () {
	set -e
	cd ${B}
	install -m 0644 db_185.h ../include/db_cxx.h db.h ${STAGING_INCDIR}/
	install -m 0755 .libs/libdb-3.2.so ${STAGING_LIBDIR}/
	ln -sf libdb-3.2.so ${STAGING_LIBDIR}/libdb-3.so
	ln -sf libdb-3.2.so ${STAGING_LIBDIR}/libdb3.so
	ln -sf libdb-3.2.so ${STAGING_LIBDIR}/libdb.so
	install -m 0644 .libs/libdb-3.2.lai ${STAGING_LIBDIR}/libdb-3.2.la
	cd ${WORKDIR}/db-${PV}/build_unix_static
	install -m 0644 libdb.a ${STAGING_LIBDIR}/
	cd ${S}
}

do_install () {
	set -e
	for i in ${B} ${WORKDIR}/db-${PV}/build_unix_static; do
	oe_runmake -C $i \
		prefix=${D}/${prefix} \
		exec_prefix=${D}/${exec_prefix} \
		bindir=${D}/${bindir} \
		includedir=${D}/${includedir} \
		libdir=${D}/${libdir} \
		docdir=${D}/${docdir} \
		install
	done
}

python do_package_prepend() {
	if oe.data.getVar('DEBIAN_NAMES', d, 1):
		oe.data.setVar('PKG_${PN}', 'libdb3', d)
}
