PV = "2.4.18-rmk7-pxa3-embedix"
LICENSE = "GPL"
KV = "2.4.18"
RMKV = "7"
PXAV = "3"
SHARPV = "20030509"
DESCRIPTION = "Linux kernel for OpenZaurus StrongArm processor based devices."

FILESDIR = "${@os.path.dirname(oe.data.getVar('FILE',d,1))}/openzaurus-sa-${KV}-rmk${RMKV}-pxa${PXAV}-embedix${SHARPV}"

SRC_URI = "http://aurach.ewu.edu/ield_software/ch5/ROM3.10_stuff/linux-sl5500-${SHARPV}-rom3_10.tar.bz2      \
           \
           file://${FILESDIR}/cacko.patch;patch=1                     \
           file://${FILESDIR}/battery.patch;patch=1                   \
           \
           file://${FILESDIR}/bluetooth-patch-2.4.18-mh9.diff;patch=1 \
           file://${FILESDIR}/iw_handlers.w13-5.diff;patch=1          \
           file://${FILESDIR}/iw_handlers.w14-5.diff;patch=1          \
           file://${FILESDIR}/iw240_we15-6.diff;patch=1               \
           \
           file://${FILESDIR}/idecs.patch;patch=1                     \
           file://${FILESDIR}/logo.patch;patch=1                      \
           file://${FILESDIR}/initsh.patch;patch=1                    \
           file://${FILESDIR}/mkdep.patch;patch=1"

# apply this when we have a patch that allows building with gcc 3.x:
# SRC_URI_append = file://${FILESDIR}/gcc-3.3.patch;patch=1
# SRC_URI_append = file://${FILESDIR}/machtune-args.patch;patch=1

S = "${WORKDIR}/linux"

inherit kernel

#FIXME: Use configuration system
export MEM = "32"
export RD = "32"
export CMDLINE = "console=ttySA root=/dev/mtdblock4 rootfstype=jffs2 jffs2_orphand_inodes=delete"
EXTRA_OEMAKE = ""

do_configure_prepend() {
        install -m 0644 ${FILESDIR}/defconfig-${MACHINE} ${S}/.config || die "No default configuration for ${MACHINE} available."

        mem=${MEM}
        rd=${RD}
        mempos=`echo "obase=16; $mem * 1024 * 1024" | bc`
        rdsize=`echo "$rd * 1024" | bc`
        total=`expr $mem + $rd`
        addr=`echo "obase=16; ibase=16; C000000 + $mempos" | bc`
        if [ "$rd" == "0" ]
        then
                echo "# CONFIG_MTD_MTDRAM_SA1100 is not set" >> ${S}/.config
        else
                echo "CONFIG_MTD_MTDRAM_SA1100=y"           >> ${S}/.config
                echo "CONFIG_MTDRAM_TOTAL_SIZE=$rdsize"     >> ${S}/.config
                echo "CONFIG_MTDRAM_ERASE_SIZE=1"           >> ${S}/.config
                echo "CONFIG_MTDRAM_ABS_POS=$addr"          >> ${S}/.config
        fi
	echo "CONFIG_CMDLINE=\"$CMDLINE mem=${mem}M\"" >> ${S}/.config
        oe_runmake oldconfig
}

do_compile() {
        cd ${S}
        test -f ${S}/.hdepend || touch ${S}/include/linux/version.h
        oe_runmake dep-files CC=${HOST_PREFIX}gcc-2.95 LD=${HOST_PREFIX}ld-2.11.2
        cd ${S}/arch/arm/tools
        ${MAKE} dep CC=${HOST_PREFIX}gcc-2.95 LD=${HOST_PREFIX}ld-2.11.2
        rm -f ${S}/include/linux/version.h
        cd ${S}
        unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS
        oe_runmake dep CC=${HOST_PREFIX}gcc-2.95 LD=${HOST_PREFIX}ld-2.11.2
        oe_runmake ${KERNEL_IMAGETYPE} CC=${HOST_PREFIX}gcc-2.95 LD=${HOST_PREFIX}ld-2.11.2
        oe_runmake modules CC=${HOST_PREFIX}gcc-2.95 LD=${HOST_PREFIX}ld-2.11.2
}
