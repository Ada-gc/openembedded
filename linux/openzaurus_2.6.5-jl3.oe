DESCRIPTION = "Linux kernel for Zaurus devices."
LICENSE = "GPL"
KV = "${@oe.data.getVar('PV',d,True).split('-')[0]}"

SRC_URI = "ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-${KV}.tar.bz2               \
	   http://www.cs.wisc.edu/~lenz/zaurus/files/patch-${PV}.diff.gz;patch=1    "
S = "${WORKDIR}/linux-${KV}"

inherit kernel

KERNEL_CCSUFFIX = "-3.3.3"
COMPATIBLE_HOST = "arm.*-linux"

#FIXME: Use configuration system
export MEM = "32"
export RD = "32"
export CMDLINE = "noinitrd root=/dev/mtdblock2 rootfstype=jffs2 console=tty0 jffs2_orphand_inodes=delete"
EXTRA_OEMAKE = ""

do_configure() {
        install -m 0644 ${FILESDIR}/defconfig-${MACHINE} ${S}/.config || die "No default configuration for ${MACHINE} available."

        mem=${MEM}
        rd=${RD}
        mempos=`echo "obase=16; $mem * 1024 * 1024" | bc`
        rdsize=`echo "$rd * 1024" | bc`
        total=`expr $mem + $rd`
        addr=`echo "obase=16; ibase=16; C000000 + $mempos" | bc`
        if [ "$rd" == "0" ]
        then
                echo "# CONFIG_MTD_MTDRAM_SA1100 is not set" >> ${S}/.config
        else
                echo "CONFIG_MTD_MTDRAM_SA1100=y"           >> ${S}/.config
                echo "CONFIG_MTDRAM_TOTAL_SIZE=$rdsize"     >> ${S}/.config
                echo "CONFIG_MTDRAM_ERASE_SIZE=1"           >> ${S}/.config
                echo "CONFIG_MTDRAM_ABS_POS=$addr"          >> ${S}/.config
        fi
	echo "CONFIG_CMDLINE=\"$CMDLINE mem=${mem}M\"" >> ${S}/.config
        oe_runmake oldconfig
}

do_compile() {
        unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS
        oe_runmake
}

