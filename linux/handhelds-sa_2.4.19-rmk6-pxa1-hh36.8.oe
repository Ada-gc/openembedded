DESCRIPTION = "handhelds.org Linux kernel for StrongArm processor based devices."
MAINTAINER = "Rene Wagner <reenoo@gmx.de>"
LICENSE = "GPL"

PACKAGE_ARCH = "ipaqsa"

FILESDIR = "${@os.path.dirname(oe.data.getVar('FILE',d,1))}/handhelds-sa-${PV}"

SRC_URI = "cvs://anoncvs:anoncvs@192.58.209.91/cvs;module=linux/kernel;tag=${@'K' + oe.data.getVar('PV',d,1).replace('.', '-')}    \
           \
           file://${FILESDIR}/mkdep.patch;patch=1"

S = "${WORKDIR}/kernel"

inherit kernel

K_MAJOR = "${@oe.data.getVar('PV',d).split('-')[0].split('.')[0]}"
K_MINOR = "${@oe.data.getVar('PV',d).split('-')[0].split('.')[1]}"
K_MICRO = "${@oe.data.getVar('PV',d).split('-')[0].split('.')[2]}"
RMKV    = "${@oe.data.getVar('PV',d).split('-')[1].split('rmk')[-1]}"
PXAV    = "${@oe.data.getVar('PV',d).split('-')[2].split('pxa')[-1]}"
HHV     = "${@oe.data.getVar('PV',d).split('-')[3].split('hh')[-1]}"

KERNEL_PRIORITY = "${@"%d" % (int(oe.data.getVar('K_MAJOR',d,1)) * 100000000 + int(oe.data.getVar('K_MINOR',d,1)) * 1000000 + int(oe.data.getVar('K_MICRO',d,1)) * 10000 + int(oe.data.getVar('RMKV',d,1)) * 1000 + int(oe.data.getVar('PXAV',d,1)) * 100 + float(oe.data.getVar('HHV',d,1)))}"

do_configure_prepend() {
	oe_runmake ipaqsa_config
        (echo N; echo m; echo N) | oe_runmake oldconfig
}
