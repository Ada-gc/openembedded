DESCRIPTION = "Linux kernel for the SIEMENS SIMpad family of devices."
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
KV = "2.4.19"
RMKV = "rmk7"
PXAV = "pxa3"
JPMV = "jpm2"
PV = "${KV}-${RMKV}-${JPMV}"

FILESDIR = "${@os.path.dirname(oe.data.getVar('FILE',d,1))}/opensimpad-${KV}-${RMKV}-${JPMV}"

SRC_URI = "ftp://ftp.kernel.org/pub/linux/kernel/v2.4/linux-${KV}.tar.bz2                                      \
           ftp://ftp.arm.linux.org.uk/pub/armlinux/source/kernel-patches/v2.4/patch-${KV}-${RMKV}.gz;patch=1   \
           \
           file://${FILESDIR}/${KV}-${RMKV}-${JPMV}.patch;patch=1     \
           \
           file://${FILESDIR}/scrolling-area.patch;patch=1            \
           file://${FILESDIR}/sound-volume-reversed.patch;patch=1     \
           \
           file://${FILESDIR}/iw_handlers.w14-5.diff;patch=1          \
           file://${FILESDIR}/iw240_we15-6.diff;patch=1               \
           \
           file://${FILESDIR}/initsh.patch;patch=1                    \
           file://${FILESDIR}/mkdep.patch;patch=1"

# apply this when we have a patch that allows building with gcc 3.x:
# SRC_URI_append = file://${FILESDIR}/gcc-3.3.patch;patch=1
# SRC_URI_append = file://${FILESDIR}/machtune-args.patch;patch=1

S = "${WORKDIR}/linux-${KV}"

inherit kernel

#FIXME: Use configuration system
export MEM = "32"
export RD = "32"
export CMDLINE = "mtdparts=sa1100:512k(boot),1m(kernel),-(root) console=tty1 root=1f02 noinitrd jffs2_orphaned_inodes=delete rootfstype=jffs2 "
EXTRA_OEMAKE = ""

do_configure_prepend() {
        install -m 0644 ${FILESDIR}/defconfig-${MACHINE} ${S}/.config

        mem=${MEM}
        rd=${RD}
        mempos=`echo "obase=16; $mem * 1024 * 1024" | bc`
        rdsize=`echo "$rd * 1024" | bc`
        total=`expr $mem + $rd`
        addr=`echo "obase=16; ibase=16; C000000 + $mempos" | bc`
        if [ "$rd" == "0" ]
        then
                echo "# CONFIG_MTD_MTDRAM_SA1100 is not set" >> ${S}/.config
        else
                echo "CONFIG_MTD_MTDRAM_SA1100=y"           >> ${S}/.config
                echo "CONFIG_MTDRAM_TOTAL_SIZE=$rdsize"     >> ${S}/.config
                echo "CONFIG_MTDRAM_ERASE_SIZE=1"           >> ${S}/.config
                echo "CONFIG_MTDRAM_ABS_POS=$addr"          >> ${S}/.config
        fi
	echo "CONFIG_CMDLINE=\"$CMDLINE mem=${mem}M\"" >> ${S}/.config
        oe_runmake oldconfig
}

do_compile_prepend() {
        cd ${S}
        test -f ${S}/.hdepend || touch ${S}/include/linux/version.h
        oe_runmake dep-files
        cd ${S}/arch/arm/tools
        ${MAKE} dep
        rm -f ${S}/include/linux/version.h
        cd ${S}
}
