DESCRIPTION = "Linux kernel for MTX-1 (MeshCube)"
MAINTAINER = "Bruno Randolf <bruno.randolf@4g-systems.biz>"
LICENSE = "GPL"
KV = "2.4.24"
PV = "${KV}"
PACKAGE_ARCH = "mtx-1"
RDEPENDS = "mtd"

SRC_URI = "cvs://anoncvs:anoncvs@meshcube.org/data/cvs;module=kernel/linux"

S = "${WORKDIR}/linux"

inherit kernel

ARCH = "mips"
KERNEL_OUTPUT = "arch/mips/zboot/images/mtx-1.flash.bin"
KERNEL_IMAGETYPE = "zImage.flash.bin"
KERNEL_IMAGEDEST = "tmp"

do_configure_prepend() {
        install -m 0644 ${S}/arch/mips/defconfig-mtx-1 ${S}/.config
}

pkg_postinst_kernel() {
if test "x$D" != "x"; then
	exit 1
else
	echo "*** flashing kernel ***"
	flashcp -v /tmp/zImage.flash.bin-${KV} /dev/mtd/2
	echo "*** done. please reboot ***"
fi
}

FILES_kernel += " /tmp"

do_deploy() {
        install -d ${DEPLOY_DIR}/images
        install -m 0644 ${KERNEL_OUTPUT} ${DEPLOY_DIR}/images/${MACHINE}-${KV}-${KERNEL_IMAGETYPE}-${DATETIME}
}

do_deploy[dirs] = "${S}"

addtask deploy before do_build after do_compile