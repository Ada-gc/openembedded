DEPENDS = "virtual/libc"
RDEPENDS = "libc6"
DESCRIPTION = "Lsof is a Unix-specific diagnostic \
tool. Its name stands for LiSt Open Files, and it \
does just that."

SRC_URI = "ftp://lsof.itap.purdue.edu/pub/tools/unix/lsof/lsof_${PV}.tar.gz"
LOCALSRC = "file://${WORKDIR}/lsof_${PV}/lsof_${PV}_src.tar"
S = "${WORKDIR}/lsof_${PV}_src"

python do_unpack () {
	oe.build.exec_func('base_do_unpack', d)
	src_uri = oe.data.getVar('SRC_URI', d)
	oe.data.setVar('SRC_URI', '${LOCALSRC}', d)
	oe.build.exec_func('base_do_unpack', d)
	oe.data.setVar('SRC_URI', src_uri, d)
}

do_configure () {
	echo PWD is $PWD
	./Configure ${TARGET_OS}
}

export I = "${STAGING_INCDIR}"
export L = "${STAGING_INCDIR}"
export EXTRA_OEMAKE =

do_compile () {
	oe_runmake 'CC=${CC}' 'CFGL=${LDFLAGS} -L./lib -llsof' 'DEBUG=' 'INCL=${CFLAGS}'
}

do_install () {
	install -d ${D}/${sbindir} ${D}/${mandir}/man8
	install -m 4755 lsof ${D}/${sbindir}/lsof
	install -m 0644 lsof.8 ${D}/${mandir}/man8/lsof.8
}
