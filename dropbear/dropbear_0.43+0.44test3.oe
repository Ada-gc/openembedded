DESCRIPTION = "Dropbear SSH Implementation"
LICENSE = "MIT"
DEPENDS = "zlib ncurses"
PR = "r2"
RV = "0.44test3"

SRC_URI = "http://matt.ucc.asn.au/dropbear/testing/dropbear-${RV}.tar.bz2 \
	   file://configure.patch;patch=1 \
	   file://allow-nopw.patch;patch=1 \
	   file://init"
S = "${WORKDIR}/dropbear-${RV}"

inherit autotools update-rc.d

INITSCRIPT_NAME = "dropbear"
INITSCRIPT_PARAMS = "defaults 10"

CFLAGS_prepend = "-I. "
LD = "${CC}"

SBINCOMMANDS = "dropbear dropbearkey dropbearconvert"
BINCOMMANDS = "dbclient ssh scp"
EXTRA_OEMAKE = 'STATIC=1 MULTI=1 SCPPROGRESS=1 PROGRAMS="${SBINCOMMANDS} ${BINCOMMANDS}"'

do_install () {
	install -d ${D}/${sysconfdir} \
		   ${D}/${sysconfdir}/init.d \
		   ${D}/${sysconfdir}/default \
		   ${D}/${sysconfdir}/dropbear \
                   ${D}/${bindir} \
		   ${D}/${sbindir} \
		   ${D}/${localstatedir}

	install -m 0755 staticdropbearmulti ${D}/${sbindir}/
	for i in ${BINCOMMANDS}
	do
		ln -s ${sbindir}/staticdropbearmulti ${D}/${bindir}/$i
	done
	for i in ${SBINCOMMANDS}
	do
		ln -s ./staticdropbearmulti ${D}/${sbindir}/$i
	done
	cat ${WORKDIR}/init | sed -e 's,/etc,${sysconfdir},g' \
				  -e 's,/usr/sbin,${sbindir},g' \
				  -e 's,/var,${localstatedir},g' \
				  -e 's,/usr/bin,${bindir},g' \
				  -e 's,/usr,${prefix},g' > ${D}/${sysconfdir}/init.d/dropbear
	chmod 755 ${D}/${sysconfdir}/init.d/dropbear
}

pkg_postinst_append () {

if [ ! -n "$D"  ]; then
if [ ! -f "${sysconfdir}/dropbear/dropbear_rsa_host_key" ]; then
        dropbearkey -t rsa -f ${sysconfdir}/dropbear/dropbear_rsa_host_key
fi
#if [ ! -f "${sysconfdir}/dropbear/dropbear_dss_host_key" ]; then
#       dropbearkey -t dss -f ${sysconfdir}/dropbear/dropbear_dss_host_key
#fi
}

pkg_postrm_append () {
  if [ -f "${sysconfdir}/dropbear/dropbear_rsa_host_key" ]; then
        rm ${sysconfdir}/dropbear/dropbear_rsa_host_key
  fi
  if [ -f "${sysconfdir}/dropbear/dropbear_dss_host_key" ]; then
        rm ${sysconfdir}/dropbear/dropbear_dss_host_key
  fi
}
