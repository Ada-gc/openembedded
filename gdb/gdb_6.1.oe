DESCRIPTION = "gdb - GNU debugger"
SECTION = "base"
PRIORITY = "optional"
MAINTAINER = "Pawel Osiczko <p.osiczko@tetrapyloctomy.org>"
DEPENDS = "ncurses"

PACKAGES =+ 'gdbserver '
FILES_gdbserver = '${bindir}/gdbserver'

inherit autotools

SRC_URI = "${GNU_MIRROR}/gdb/gdb-${PV}.tar.gz \
	  file://signalnamespace.patch;patch=1"

LDFLAGS_append = " -s"
export CC_FOR_BUILD = "${BUILD_CC}"
export CXX_FOR_BUILD = "${BUILD_CXX}"
export CPP_FOR_BUILD = "${BUILD_CPP}"
export CFLAGS_FOR_BUILD = "${BUILD_CFLAGS}"
export CXXFLAGS_FOR_BUILD = "${BUILD_CXXFLAGS}"
export CPPFLAGS_FOR_BUILD = "${BUILD_CPPFLAGS}"
export CFLAGS_append=" -L${STAGING_LIBDIR}"
EXTRA_OEMAKE = "'SUBDIRS=intl mmalloc libiberty opcodes bfd sim gdb etc utils'"

EXTRA_OECONF = "--disable-gdbtk --disable-tui --disable-x --disable-nls \
                --with-curses --with-readline --disable-sim \
                --program-prefix=''"

do_configure () {
# override this function to avoid the autoconf/automake/aclocal/autoheader
# calls for now
	gnu-configize
	oe_runconf
}

do_install () {
	oe_runmake install \
	    'prefix=${D}' 'exec_prefix=${D}' 'bindir=${D}/bin' \
	    'sbindir=${D}/sbin' 'infodir=${D}/share/info' 'libdir=${D}/lib' \
	    'mandir=${D}/share/man' 'includedir=${D}/include'
	install -d ${D}${bindir}
	install -m 0755 gdb/gdbserver/gdbserver ${D}${bindir}
}

#
# patch description
#
# readline.patch:
#   gdb 5.3 provides its own readline source which tends to conflict with
#   readline package. we override readline included from gdb source
#   with packaged readline and fix up extern tilde_expand in gdb/defs.h
#
