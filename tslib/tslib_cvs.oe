SECTION = "base"
DESCRIPTION = "tslib is a touchscreen access library."
PV = "0.0cvs${CVSDATE}"
PR = "r8"

SRC_URI = "cvs://cvs:@pubcvs.arm.linux.org.uk/mnt/src/cvsroot;module=tslib \
	   file://ts.conf \
	   file://tslib.sh \
	   file://initialize_djs.patch;patch=1 "
SRC_URI_append_ramses += " file://devfs.patch;patch=1"
SRC_URI_append_ramses += " file://event1.patch;patch=1"
S = "${WORKDIR}/tslib"

CONFFILES_${PN} = "${sysconfdir}/ts.conf"

inherit autotools

PACKAGES = "libts libts-dev tslib-tests tslib-calibrate"
EXTRA_OECONF = "--enable-shared"
EXTRA_OECONF_append_ramses = " --enable-input --disable-ucb1x00 --disable-corgi --disable-collie --disable-h3600 --disable-mk712 --disable-arctic2"
EXTRA_OECONF_beagle = "--enable-shared --enable-h3600 --disable-input --disable-corgi --disable-collie --disable-mk712 --disable-arctic2 --disable-ucb1x00 "

do_stage () {
	oe_libinstall -so -C src libts-0.0 ${STAGING_LIBDIR}
	ln -sf libts-0.0.so ${STAGING_LIBDIR}/libts.so
	install -m 0644 src/tslib.h ${STAGING_INCDIR}/
	install -m 0644 src/tslib-private.h ${STAGING_INCDIR}/
}

do_install_prepend () {
	install -m 0644 ${WORKDIR}/ts.conf ${S}/etc/ts.conf
}

do_install_append() {
	install -d ${D}/${sysconfdir}/profile.d/
	install -m 0755 ${WORKDIR}/tslib.sh ${D}/${sysconfdir}/profile.d/
}

FILES_libts  = "${sysconfdir}/ts.conf ${sysconfdir}/profile.d/tslib.sh "
FILES_libts += "${libdir}/*.so.* ${libdir}/ts/*.so*"
FILES_libts-dev = "${FILES_tslib-dev}"
FILES_tslib-calibrate += "${bindir}/ts_calibrate"
FILES_tslib-tests = "${bindir}/ts_harvest ${bindir}/ts_print ${bindir}/ts_print_raw ${bindir}/ts_test"
