DESCRIPTION = "KDE-Pim Platform Independent - port of KDE Pim to PDA"
DESCRIPTION_kopi = "Korganizer/PI is a powerful calendar and ToDo Tool"
DESCRIPTION_kopi-applet = "KDE Pim/PI applet"
DESCRIPTION_kapi = "KAddressbook/PI is a port of KDE addressbook"
DESCRIPTION_libmicrokdelibs    = "KDE Pim/PI library - microkde"
DESCRIPTION_libmicrokcal       = "KDE Pim/PI library - microkcal"
DESCRIPTION_libmicrokabc       = "KDE Pim/PI library - microkabc"

SECTION = "base"
PRIORITY = "optional"
MAINTAINER = "Marcin Juszkiewicz <openembedded@hrw.one.pl>"
LICENSE = "GPL"
HOMEPAGE = "http://www.pi-sync.net/ http://sf.net/projects/kdepimpi/"
#DEPENDS = "gammu bluez-libs libetpan libmailwrapper"
# gammu as replacement for libmicrogammu
# bluez-libs for libbluetooth
# libetpan & libmailwrapper as replacement for kdepim/pi versions
# will be needed for KMicroMail/PI
PR = "r1"

DEFAULT_PREFERENCE = "-1"

SRC_URI = "${SOURCEFORGE_MIRROR}/kdepimpi/kdepim_1.9.4.src.tar.gz \
file://qinputdialog_inc.patch;patch=1 \
file://manager.h.patch;patch=1 \
file://libkcal.pro.patch;patch=1 \
"

S = "${WORKDIR}/kdepim"

EXTRA_QMAKEVARS_POST += "KDEPIMDIR=${S} INCLUDEPATH+=../ OBJECTS_DIR=obj/ MOC_DIR=moc/ \
			 LIBS+=-L${S}/dest/ DESTDIR=${S}/dest LIBS+=-lstdc++ \
			 LIBS+=-Wl,-rpath-link,${S}/dest"

# That changes will be needed for KMicroMail/PI to get it build with OE libs
#
#             LIBS-=-lkmicromailwrapper LIBS+=-lmailwrapper LIBS+=-lopiecore2 \
#             LIBS-=-lkmicrolibetpan LIBS+=-letpan \

QMAKE_PROFILES= "all.pro"

# we can't build:
# kabc/plugins/qtopia   - wants qpe/pim/  (Qtopia)
# kabc/plugins/sharpdtm - wants libsl     (Sharp ROM 3.x closed source library)
# kabc/plugins/ldap     - wants ldap libs (?)
# kabc/plugins/opie     - wants libopie1  (we don't plan to have it in OE)
# kmicromail            - have to check changes in KDEPIM/PI version libmailwrapper

SUBDIRS="libical/src/libical libical/src/libicalss microkde libkcal libkdepim \
		kabc korganizer kalarmd kaddressbook kabc/plugins/file kabc/plugins/dir \ 
		kabc/formats/binary "

# TODO:
# RDEPENDS_kapi = "libmicrokabc_dir libmicrokabc_file libmicrokabcformat_binary"
# checking libmailwrapper changes
# author said that he didn't changed libetpan so we probably can get OE one
# compile opie mail/pi

inherit palmtop

do_compile() {
	export KDEPIMDIR=${S}
	oe_runmake
}

do_configure_prepend() {
		mv ${S}/kabc/formats/binary/kabcformat_binaryE.pro ${S}/kabc/formats/binary/binaryE.pro
        echo -e "TEMPLATE=subdirs\nSUBDIRS=qtcompat ${SUBDIRS}\n" >all.pro
        for d in ${SUBDIRS}
        do
                mv -f ${d}/`basename ${d}`E.pro ${d}/`basename ${d}`.pro
        done
		mkdir "${S}/dest"
}

do_install() {
    install -d ${D}/${palmtopdir}/bin \
               ${D}/${palmtopdir}/lib \
               ${D}/${palmtopdir}/apps/1Pim \
               ${D}/${palmtopdir}/pics/kdepim/korganizer/icons16 \
               ${D}/${palmtopdir}/pics/kdepim/korganizer/iconsmini \
               ${D}/${palmtopdir}/pics/kdepim/kaddressbook/icons16 \
               ${D}/${palmtopdir}/pics/kdepim/kaddressbook/icons22 \
               ${D}/${palmtopdir}/plugins/applets

    install -m 0644 ${S}/bin/kdepim/korganizer/ko16.png ${D}/${palmtopdir}/pics/
    install -m 0644 ${S}/bin/kdepim/korganizer/ko24.png ${D}/${palmtopdir}/pics/
    install -m 0644 ${S}/bin/kdepim/korganizer/*.* ${D}/${palmtopdir}/pics/kdepim/korganizer
    install -m 0644 ${S}/bin/kdepim/korganizer/icons16/*.png ${D}/${palmtopdir}/pics/kdepim/korganizer/icons16
    install -m 0644 ${S}/bin/kdepim/korganizer/iconsmini/*.png ${D}/${palmtopdir}/pics/kdepim/korganizer/iconsmini
    install -m 0644 korganizer/korganizer.desktop ${D}/${palmtopdir}/apps/1Pim/korganizer.desktop

    install -m 0644 ${S}/bin/kdepim/kaddressbook/zone.tab ${D}/${palmtopdir}/pics/kdepim/kaddressbook
    install -m 0644 ${S}/bin/kdepim/kaddressbook/*.* ${D}/${palmtopdir}/pics/kdepim/kaddressbook
    install -m 0644 ${S}/bin/kdepim/kaddressbook/icons16/*.png ${D}/${palmtopdir}/pics/kdepim/kaddressbook/icons16
    install -m 0644 ${S}/bin/kdepim/kaddressbook/icons22/*.png ${D}/${palmtopdir}/pics/kdepim/kaddressbook/icons22
    install -m 0644 kaddressbook/kaddressbook.desktop ${D}/${palmtopdir}/apps/1Pim/kaddressbook.desktop

    for f in libmicrokde libmicrokdepim libmicrokabc libmicrokcal libmicroqtcompat libmicrokabc_file libmicrokabc_dir libmicrokabcformat_binary
    do
        oe_libinstall -so -C ${S}/dest/ $f ${D}/${palmtopdir}/lib
    done

    install -m 0755 ${S}/dest/kopi ${D}/${palmtopdir}/bin/kopi
    install -m 0755 ${S}/dest/kapi ${D}/${palmtopdir}/bin/kapi

    for f in libkopialarmapplet
    do
        oe_libinstall -so -C ${S}/dest/ $f ${D}/${palmtopdir}/plugins/applets
    done
}

PACKAGES = "kopi-applet kopi kapi libmicrokabc libmicrokcal libmicrokdelibs "

FILES_kopi = "${palmtopdir}/bin/kopi ${palmtopdir}/apps/1Pim/korganizer.desktop ${palmtopdir}/pics/kdepim/korganizer/* "
FILES_kapi = "${palmtopdir}/bin/kapi ${palmtopdir}/apps/1Pim/kaddressbook.desktop ${palmtopdir}/pics/kdepim/kaddressbook/* ${palmtopdir}/lib/libmicrokabc_* ${palmtopdir}/lib/libmicrokabcformat_binary*"
FILES_kopi-applet = "${palmtopdir}/plugins/applets/* ${palmtopdir}/pics/ko16.png ${palmtopdir}/pics/ko24.png"
FILES_libmicrokdelibs = "${palmtopdir}/lib/*"
FILES_libmicrokabc = "${palmtopdir}/lib/libmicrokabc*"
FILES_libmicrokcal = "${palmtopdir}/lib/libmicrokcal*"
