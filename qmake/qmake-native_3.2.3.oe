SECTION = "devel"
DESCRIPTION = "Qt(E) Makefile Generator"
PRIORITY = "optional"
LICENSE = "GPL/QPL"

SRC_URI = \
	"ftp://ftp.trolltech.com/pub/qt/source/qt-embedded-free-${PV}.tar.bz2 \
	file://linux-oe-qmake.conf"

S = "${WORKDIR}/qt-embedded-free-${PV}"

inherit autotools native

export QTDIR = "${S}"

EXTRA_OECONF = "-static -no-gif -no-sm -system-zlib -system-libjpeg \
	-system-libpng -no-thread -no-nis -no-cups -no-stl -disable-styles \
	-disable-tools -disable-kernel -disable-widgets -disable-dialogs \
	-disable-iconview -disable-workspace -disable-network -disable-canvas \
	-disable-table -disable-xml -disable-opengl -disable-sql"
EXTRA_OEMAKE = "-e"

do_configure() {
	# Install the OE build templates (linux, linux-uclibc)
	install -d ${S}/mkspecs/linux-oe-g++
	install -d ${S}/mkspecs/linux-uclibc-oe-g++
	install -m 0644 ${WORKDIR}/linux-oe-qmake.conf \
		${S}/mkspecs/linux-oe-g++/qmake.conf
	ln -sf ../linux-g++/qplatformdefs.h \
		${S}/mkspecs/linux-oe-g++/qplatformdefs.h
	ln -sf ../linux-oe-g++/qmake.conf \
		${S}/mkspecs/linux-uclibc-oe-g++/qmake.conf
	ln -sf ../linux-g++/qplatformdefs.h \
		${S}/mkspecs/linux-uclibc-oe-g++/qplatformdefs.h

	QMAKESPEC=
	PLATFORM=${HOST_OS}-oe-g++
	export PLATFORM
	oenote ./configure ${EXTRA_OECONF}
	echo yes | ./configure ${EXTRA_OECONF} || die "Configuring qt failed"
}

do_compile() {
	:
}

do_stage() {
	install -m 0755 bin/qmake ${STAGING_BINDIR}
	install -d ${QMAKE_MKSPEC_PATH}
	cp -dfR mkspecs/* ${QMAKE_MKSPEC_PATH}
}

do_install() {
        :
}
