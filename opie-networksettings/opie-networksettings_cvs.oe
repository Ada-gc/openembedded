DESCRIPTION = "Opie Network Settings"
SECTION = "opie/settings"
PRIORITY = "optional"
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
PROVIDES = "opie-networksettings-pppplugin opie-networksettings-wlanplugin"
PV = "1.1.7+cvs-${CVSDATE}"

APPNAME = "networksettings"

SRC_URI = "${HANDHELDS_CVS};module=opie/noncore/settings/networksettings \
           ${HANDHELDS_CVS};module=opie/pics \
           ${HANDHELDS_CVS};module=opie/apps \
	   ${HANDHELDS_CVS};module=opie/root"

S = "${WORKDIR}/${APPNAME}"

inherit opie

EXTRA_QMAKEVARS_POST += "LIBS+=-L${S}"

do_install() {
	# Create dir for changedns and the opie-kppp peer
	install -d ${D}/${bindir}/
	install -d ${D}/${sysconfdir}/
	install -d ${D}/${sysconfdir}/ppp/
	install -d ${D}/${sysconfdir}/ppp/peers/
	
	install -d ${D}/${palmtopdir}/pics/${APPNAME}/
	install -d ${D}/${palmtopdir}/pics/Network/
	install -m 0644 ${WORKDIR}/pics/${APPNAME}/*.* ${D}${palmtopdir}/pics/${APPNAME}/
	install -m 0644 ${WORKDIR}/pics/Network/*.* ${D}${palmtopdir}/pics/Network/
	install -d ${D}/${palmtopdir}/lib/
	oe_libinstall -so libinterfaces ${D}${palmtopdir}/lib
	install -d ${D}/${palmtopdir}/plugins/networksettings/
	for plugin in kppp wlan
	do
		install lib$plugin.so ${D}/${palmtopdir}/plugins/networksettings/
	done

	# install changedns script
	install -m 0755 ${WORKDIR}/root/usr/bin/changedns ${D}${bindir}/
	install -m 0644 ${WORKDIR}/root/etc/ppp/peers/opie-kppp ${D}${sysconfdir}/ppp/peers
}

PACKAGES =+ "opie-networksettings-pppplugin opie-networksettings-wlanplugin"
FILES_${PN} += " ${bindir}/changedns "
FILES_opie-networksettings-pppplugin = "${palmtopdir}/plugins/networksettings/libkppp.so ${sysconfdir}/ppp/peers/opie-kppp"
FILES_opie-networksettings-wlanplugin = "${palmtopdir}/plugins/networksettings/libwlan.so"

