DESCRIPTION = "Qt/X11 Version ${PV}"
SECTION = "libs"
PRIORITY = "optional"
LICENSE = "GPL/QPL"
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
DEPENDS = "uicmoc3-native freetype x11 xft xext libxrender libxrandr libxcursor"
PR = "r3"

SRC_URI = "ftp://ftp.trolltech.com/qt/source/qt-x11-free-${PV}.tar.bz2 \
	   file://configure.patch;patch=1 \
	   file://no-examples.patch;patch=1"
S = "${WORKDIR}/qt-x11-free-${PV}"

inherit qmake-base

export QTDIR = "${S}"
ARCH_i686 = "x86"
EXTRA_OEMAKE = "-e"

QT_CONFIG_FLAGS = "-release -shared -qt-zlib -no-nas-sound -no-sm -qt-libpng -no-gif -no-xinerama \
                   -no-tablet -no-xkb -no-dlopen-opengl -no-nis -no-cups -thread"

do_configure() {
	echo "yes" | ./configure -prefix ${prefix} ${QT_CONFIG_FLAGS} -fast \
		-L${STAGING_LIBDIR} -I${STAGING_INCDIR} -I${STAGING_INCDIR}/freetype2
	# force regenerate
	rm src/qtmain.pro
	cat Makefile >makefile
	find . -name "Makefile"|xargs rm -f
	cd src && qmake -spec ${QMAKESPEC}
}

do_compile() {
	unset CFLAGS
	unset CXXFLAGS
#	cd src && oe_runmake QMAKESPEC="${QMAKESPEC}" QMAKE="${STAGING_BINDIR}/qmake" MOC="moc3" UIC="uic3" MAKE="make -e"
	cd src && oe_runmake \
		QMAKE="${STAGING_BINDIR}/qmake -after INCPATH+=${STAGING_INCDIR} \
		INCPATH+=${STAGING_INCDIR}/freetype2 LIBS+=-L${STAGING_LIBDIR}" \
		QMAKESPEC="${QMAKESPEC}" LINK="${CXX} -Wl,-rpath-link,${STAGING_LIBDIR}" \
		MOC="${STAGING_BINDIR}/moc3" UIC="${STAGING_BINDIR}/uic3" MAKE="make -e"
}

do_stage() {
	oe_soinstall lib/libqt-mt.so.${PV} ${STAGING_LIBDIR}/
	install -d ${STAGING_INCDIR}/private
	for f in include/*.h
	do
		install -m 0644 $f ${STAGING_INCDIR}/
	done
	for f in include/private/*.h
	do
	        install -m 0644 $f ${STAGING_INCDIR}/private/
	done
}

do_install() {
	install -d ${D}/${libdir}/
	oe_soinstall lib/libqt-mt.so.${PV} ${D}/${libdir}/
}
