DESCRIPTION = "Base libraries for the Open Integrated Palmtop Environment"
HOMEPAGE = "http://opie.handhelds.org"
SECTION = "opie/libs"
PRIORITY = "optional"
MAINTAINER = "Michael Lauer <mickey@Vanille.de>"
LICENSE = "LGPL"
DEPENDS = "virtual/libc libqpe-opie sqlite libpcap-0.7.2"
PROVIDES = "libopiecore2 libopiedb2 libopiemm2 libopienet2 libopiepim2 libopieui2"
PV = "1.1.4-cvs-${CVSDATE}"

SRC_URI = "cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/libopie2;date=${CVSDATE}"
S = "${WORKDIR}/libopie2"

inherit qmake

export QTDIR = "${STAGING_LIBDIR}/.."
export OPIEDIR = "${STAGING_LIBDIR}/.."

#FIXME: Add machine specific bits here. Best use oe_machinstall and read from a given file

EXTRA_QMAKEVARS_POST = ' DEFINES+=OPIE_NEW_MALLOC    \
                         DEFINES+="OPIE_SOUND_FRAGMENT_SHIFT=16" \
                         DEFINES+="OPIE_WE_VERSION=15" '

do_compile() {
	install -d ${STAGING_INCDIR}/opie2
	for i in `find . -name "*.h"`
	do
		install -m 0644 $i ${STAGING_INCDIR}/opie2/`basename $i`
	done
	oe_runmake MOC=${STAGING_BINDIR}/moc UIC=${STAGING_BINDIR}/uic DESTDIR=${S}
}

# no sophisticated staging neessary, because most include+lib files already done during do_compile
# only need some special case for uic-created header files here

do_stage() {
	install -m 0644 ${FILESDIR}/include.pro ${OPIEDIR}/
	install -m 0644 opiepim/ui/opimrecurrencebase.h ${STAGING_INCDIR}/opie2/
	install -m 0644 opieui/otimepickerbase.h ${STAGING_INCDIR}/opie2/
	install -m 0644 opiepim/core/opimtemplatebase.h ${STAGING_INCDIR}/opie2/
}

do_install() {
	oe_libinstall -so -C ${STAGING_LIBDIR} libopiecore2 ${D}/${palmtopdir}/lib
	oe_libinstall -so -C ${STAGING_LIBDIR} libopiedb2 ${D}/${palmtopdir}/lib
	oe_libinstall -so -C ${STAGING_LIBDIR} libopiemm2 ${D}/${palmtopdir}/lib
	oe_libinstall -so -C ${STAGING_LIBDIR} libopienet2 ${D}/${palmtopdir}/lib
	oe_libinstall -so -C ${STAGING_LIBDIR} libopiepim2 ${D}/${palmtopdir}/lib
	oe_libinstall -so -C ${STAGING_LIBDIR} libopieui2 ${D}/${palmtopdir}/lib
}

PACKAGES = "libopiecore2 libopiedb2 libopiemm2 libopienet2 libopiepim2 libopieui2"
FILES_libopiecore2 = "${palmtopdir}/lib/libopiecore2.so.*"
FILES_libopiedb2 = "${palmtopdir}/lib/libopiedb2.so.*"
FILES_libopiemm2 = "${palmtopdir}/lib/libopiemm2.so.*"
FILES_libopienet2 = "${palmtopdir}/lib/libopienet2.so.*"
FILES_libopiepim2 = "${palmtopdir}/lib/libopiepim2.so.*"
FILES_libopieui2 = "${palmtopdir}/lib/libopieui2.so.*"

#FIXME: Add individual package descriptions and dependencies

