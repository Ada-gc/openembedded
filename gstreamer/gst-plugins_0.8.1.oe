DESCRIPTION = "Plugins for GStreamer"
SECTION = "x11/libs"
PRIORITY = "optional"
MAINTAINER = "Phil Blundell <pb@handhelds.org>"
PR = "r3"

DEPENDS = "gstreamer zlib libmikmod libmad libogg tremor libvorbis libid3tag gnome-vfs esound-gpe"

SRC_URI = "http://gstreamer.freedesktop.org/src/gst-plugins/gst-plugins-${PV}.tar.bz2 \
	   file://ivorbis.patch;patch=1"

EXTRA_OECONF = "--disable-docs-build --disable-dependency-tracking --disable-aalib --disable-arts --disable-artsc --disable-libfame --disable-sdl"

inherit autotools pkgconfig gconf

acpaths = "-I ${S}/common/m4 -I ${S}/m4"

LIBV = "0.8"

plugin_postinst() {
gst-register --gst-registry=/var/cache/gstreamer-0.8/registry.xml
}

python populate_packages_prepend () {
	gst_libdir = oe.data.expand('${libdir}/gstreamer-${LIBV}', d)
	postinst = oe.data.getVar('plugin_postinst', d, 1)

	do_split_packages(d, gst_libdir, '^libgst(.*)\.so$', 'gst-plugin-%s', 'GStreamer plugin for %s', postinst=postinst)
	do_split_packages(d, gst_libdir, '^libgst(.*)\.l?a$', 'gst-plugin-%s-dev', 'GStreamer plugin for %s (development files)')
}

do_stage() {
	install -d ${STAGING_INCDIR}/gstreamer-0.8/gst/gconf/
	install -m 0644 gst-libs/gst/gconf/gconf.h ${STAGING_INCDIR}/gstreamer-0.8/gst/gconf/
	oe_libinstall -C gst-libs/gst/gconf -so libgstgconf-0.8 ${STAGING_LIBDIR}
}
