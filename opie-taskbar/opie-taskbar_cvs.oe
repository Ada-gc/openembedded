DESCRIPTION = "Opie Launcher"
SECTION = "opie/base"
PRIORITY = "optional"
LICENSE = "GPL"
DEPENDS = "opie-libqrsync "
PV = "1.1.4-cvs-${CVSDATE}"
APPNAME = "qpe"
PR = "3"

SRC_URI = "cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/core/apps/calibrate;date=${CVSDATE} \
           cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/noncore/settings/mediummount;date=${CVSDATE} \
           cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/core/launcher;date=${CVSDATE} \
           cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/pics;date=${CVSDATE} \
           cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/apps;date=${CVSDATE} \
           cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/root;date=${CVSDATE} \
           file://server.pro \
           file://opie-reorgfiles"

S = "${WORKDIR}/launcher"

inherit opie update-rc.d

INITSCRIPT_NAME = "opie"
INITSCRIPT_PARAMS = "defaults 99"

do_configure_prepend() {
	ln -s ${WORKDIR}/calibrate ${S}/calibrate
	ln -s ${WORKDIR}/mediummount ${S}/mediummount
	install -m 0644 ${WORKDIR}/server.pro ${S}/server.pro
}

do_stage() {
    install obexinterface.h ${STAGING_INCDIR}/

}
do_install() {
	install -d ${D}/${palmtopdir}/bin \
		   ${D}/${palmtopdir}/apps/Applications \
		   ${D}/${palmtopdir}/apps/Settings \
		   ${D}/${palmtopdir}/pics/launcher \
		   ${D}/etc/init.d
	install -m 0755 ${S}/qpe ${D}/${palmtopdir}/bin/qpe
	install -m 0755 ${WORKDIR}/opie-reorgfiles ${D}/${palmtopdir}/bin/
	install -m 0644 ${WORKDIR}/apps/Settings/calibrate.desktop ${D}/${palmtopdir}/apps/Settings/calibrate.desktop
	install -m 0644 ${WORKDIR}/pics/launcher/*.* ${D}/${palmtopdir}/pics/launcher/
	install -m 0755 ${WORKDIR}/root/etc/init.d/opie ${D}/etc/init.d/opie
#FIXME use our own - machine dependent - start script
#FIXME why is this still called qpe? it should be called launcher, or taskbar, or opie, or whatever - just not QPE!
}

FILES_opie-taskbar_append = " /etc/init.d/opie"
