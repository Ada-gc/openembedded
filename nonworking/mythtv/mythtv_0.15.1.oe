DESCRIPTION = "A full featured personal video recorder system."
MAINTAINER = "Michael 'Mickey' Lauer"
SECTION = "multimedia"
PR = "r1"

SRC_URI = "http://www.mythtv.org/mc/mythtv-0.15.1.tar.bz2 \
	   file://settings.patch;patch=1"

inherit qmake

QMAKE_PROFILES = "mythtv.pro"

def mythtv_arch(d):
        import oe, re
        arch = oe.data.getVar('TARGET_ARCH', d, 1)
        if re.match("^i.86$", arch):
                arch = "x86"
        elif arch == "x86_64":
                arch = "x86"
        elif arch == "arm":
                arch = "armv4l"
        return arch

MYTHTV_ARCH := "${@mythtv_arch(d)}"

EXTRA_QMAKEVARS_POST += "QMAKE_LIBS_QT=-lqt QMAKE_LIBS_QT_THREAD=-lqt-mt"

do_configure_prepend() {
# it's not autotools anyway, so we call ./configure directly
	find . -name "Makefile"|xargs rm -f
	./configure	--prefix=/usr		\
			--mandir=/usr/man 	\
			--disable-mp3lame	\
			--enable-vorbis 	\
			--disable-faad		\
			--disable-faadbin 	\
			--disable-faac		\
			--disable-mingw32	\
			--disable-a52		\
			--disable-a52bin	\
			--enable-pp		\
			--enable-shared-pp	\
			--enable-shared		\
			--disable-amr_nb	\
			--disable-amr_nb-fixed	\
			--disable-sunmlib	\
						\
			--cpu=${MYTHTV_ARCH}	\
			--disable-mmx		\
			--disable-altivec	\
			--enable-v4l		\
			--enable-audio-oss	\
			--disable-audio-beos	\
			--enable-dv1394		\
			--enable-network	\
			--enable-zlib		\
			--enable-simple_idct	\
			--disable-vhook		\
			--disable-mpegaudio-hp	\
			--enable-ffserver	\
			--enable-ffplay		\
			--enable-risky
}

