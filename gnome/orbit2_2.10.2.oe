DESCRIPTION = "CORBA ORB"
SECTION = "gnome/base"
SRC_URI = "http://ftp.gnome.org/pub/GNOME/sources/ORBit2/2.10/ORBit2-${PV}.tar.bz2 \
	   file://${FILESDIR}/gtk-doc.m4 \
	   file://${FILESDIR}/configure-lossage.patch;patch=1;pnum=1"
DEPENDS = "libidl popt orbit2-native"

S = "${WORKDIR}/ORBit2-${PV}"

inherit autotools  pkgconfig

do_compile() {
	oe_runmake IDL_COMPILER="`which orbit-idl-2`"
}

do_configure_prepend() {
	install -d m4
	install ${FILESDIR}/gtk-doc.m4 m4/
}

do_stage() {
	oe_libinstall -so -C src/orb libORBit-2 ${STAGING_LIBDIR}
	oe_libinstall -so -C src/services/name libORBitCosNaming-2 ${STAGING_LIBDIR}
	oe_libinstall -a -C src/services/name libname-server-2 ${STAGING_LIBDIR}
	oe_libinstall -so -C src/services/imodule libORBit-imodule-2 ${STAGING_LIBDIR}

	for dir in orbit orbit/poa orbit/orb-core orbit/util orbit/dynamic; do
		install -d ${STAGING_INCDIR}/orbit-2.0/$dir
		( cd include/$dir; for i in *.h; do install -m 0644 $i ${STAGING_INCDIR}/orbit-2.0/$dir/$i; done )
	done

	install -d ${STAGING_INCDIR}/orbit-2.0/ORBitservices
	install -m 0644 src/services/name/CosNaming.h ${STAGING_INCDIR}/orbit-2.0/ORBitservices/
	install -m 0644 src/services/name/CosNaming_impl.h ${STAGING_INCDIR}/orbit-2.0/ORBitservices/
	install -m 0644 src/services/imodule/orbit-imodule.h ${STAGING_INCDIR}/orbit-2.0/ORBitservices/
}
