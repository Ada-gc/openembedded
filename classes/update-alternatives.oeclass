# defaults
ALTERNATIVE_PRIORITY = "10"
ALTERNATIVE_LINK = "${bindir}/${ALTERNATIVE_NAME}"

update_alternatives_postinst() {
update-alternatives --install ${ALTERNATIVE_LINK} ${ALTERNATIVE_NAME} ${ALTERNATIVE_PATH} ${ALTERNATIVE_PRIORITY}
}

update_alternatives_postrm() {
update-alternatives --remove ${ALTERNATIVE_NAME} ${ALTERNATIVE_PATH}
}

python __anonymous() {
	if oe.data.getVar('ALTERNATIVE_NAME', d) == None:
		raise oe.build.FuncFailed, "%s inherits update-alternatives but doesn't set ALTERNATIVE_NAME" % oe.data.getVar('FILE', d)
	if oe.data.getVar('ALTERNATIVE_PATH', d) == None:
		raise oe.build.FuncFailed, "%s inherits update-alternatives but doesn't set ALTERNATIVE_PATH" % oe.data.getVar('FILE', d)
}

python populate_packages_prepend () {
	pkg = oe.data.getVar('PN', d, 1)
	oe.note('adding update-alternatives calls to postinst/postrm for %s' % pkg)
	postinst = oe.data.getVar('pkg_postinst_%s' % pkg, d, 1) or oe.data.getVar('pkg_postinst', d, 1)
	if not postinst:
		postinst = '#!/bin/sh\n'
	postinst += oe.data.getVar('update_alternatives_postinst', d, 1)
	oe.data.setVar('pkg_postinst_%s' % pkg, postinst, d)
	postrm = oe.data.getVar('pkg_postrm_%s' % pkg, d, 1) or oe.data.getVar('pkg_postrm', d, 1)
	if not postrm:
		postrm = '#!/bin/sh\n'
	postrm += oe.data.getVar('update_alternatives_postrm', d, 1)
	oe.data.setVar('pkg_postrm_%s' % pkg, postrm, d)
}
