DESCRIPTION = "Utilities and system configuration files for the Linux PCMCIA card services"
SECTION = "base"
PRIORITY = "required"
LICENSE = "GPL"
DEPENDS = "virtual/kernel"
PR = "r1"

SRC_URI = "${SOURCEFORGE_MIRROR}/pcmcia-cs/pcmcia-cs-${PV}.tar.gz"
S = "${WORKDIR}/pcmcia-cs-${PV}"

INITSCRIPT_NAME = "pcmcia"
INITSCRIPT_PARAMS = "defaults"

inherit update-rc.d

export KERNEL_SOURCE = ${@base_read_file('${STAGING_DIR}/${HOST_SYS}/kernel/kernel-source')}

sbindir = "/sbin"

do_configure() {
	touch .prereq.ok
	touch config.out
	cat >config.mk <<EOF
UCC=${CC}
UFLAGS=${CFLAGS} -I${S}/include
HAS_WORDEXP=y
SYSV_INIT=y
RC_DIR=/etc
CONFIG_PNP_BIOS=n
ARCH=${ARCH}
CONFIG_CARDBUS=n
CONFIG_PCMCIA=y
CONFIG_INET=y
CONFIG_SCSI=y
DO_IDE=y
EOF
	cat >include/pcmcia/autoconf.h <<EOF
#define HAS_WORDEXP 1
EOF
}

do_compile() {
        oe_runmake all HAS_XPM= FLIBS="" XMANDIR=""
}

INSTALL_ETC = "ftl ide ieee1394 memory network parport scsi serial wireless"
INSTALL_ETC_DATA = "config config.opts ftl.opts ide.opts ieee1394.opts memory.opts network.opts parport.opts scsi.opts serial.opts shared wireless.opts"

do_install() {
	install -d ${D}/${sbindir}
	for f in cardmgr/cardctl cardmgr/cardmgr cardmgr/ide_info cardmgr/ifport cardmgr/ifuser cardmgr/pcinitrd flash/ftl_check flash/ftl_format
	do
		install -m 0755 $f ${D}/${sbindir}/
	done
	install -d ${D}/${sysconfdir}/init.d \
		   ${D}/${sysconfdir}/pcmcia \
		   ${D}/${sysconfdir}/pcmcia/cis
	for i in ${INSTALL_ETC}; do
		install etc/${i} ${D}/${sysconfdir}/pcmcia/
	done
	for i in ${INSTALL_ETC_DATA}; do
		install -m 0644 etc/${i} ${D}/${sysconfdir}/pcmcia/
	done
	for i in etc/cis/*; do
		install -m 0644 $i ${D}/${sysconfdir}/pcmcia/cis/
	done
	install -m 0755 etc/rc.pcmcia ${D}/${sysconfdir}/init.d/pcmcia
}

FILES_${PN} = "${sbindir} ${sysconfdir}"
