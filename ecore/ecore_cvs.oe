DESCRIPTION = "Ecore is the core event abstraction layer for the enlightenment \
foundation libraries. It makes makes doing selections, drag and drop, event loops, \
timeouts and idle handlers fast, optimized, and convenient."
HOMEPAGE = "http://www.enlightenment.org"
SECTION = "libs"
PRIORITY = "optional"
PV = "${CVSDATE}"
DEPENDS = "edb eet evas freetype edb-native"
PR = "r1"

SRC_URI = "cvs://anonymous@cvs.sourceforge.net/cvsroot/enlightenment;module=e17/libs/ecore"
S = "${WORKDIR}/ecore"

inherit autotools

export EDB_CONFIG = "${STAGING_BINDIR}/edb-config"
export EET_CONFIG = "${STAGING_BINDIR}/eet-config"
export EVAS_CONFIG = "${STAGING_BINDIR}/evas-config"
export FREETYPE_CONFIG = "${STAGING_BINDIR}/freetype-config"

EXTRA_OECONF = "--enable-ecore-fb \
		--enable-ecore-job \
		--enable-ecore-evas-fb \
		--disable-ecore-evas-x \
		--disable-ecore-evas-gl \
		--enable-ecore-con \
		--enable-ecore-ipc \
		--enable-ecore-txt \
		--disable-ecore-x \
		--enable-ecore-config \
		--disable-openssl"

parts = "Ecore Ecore_Job \
	 Ecore_Txt Ecore_Fb Ecore_Con \
	 Ecore_Ipc Ecore_Evas Ecore_Config"

do_stage () {
	for p in ${parts}; do
		dir=`echo $p|tr A-Z a-z`
		install -m 0644 ${S}/src/lib/$dir/$p.h ${STAGING_INCDIR}/
		oe_libinstall -C src/lib/$dir lib$dir ${STAGING_LIBDIR}/
	done
	install -m 0644 ${S}/src/lib/ecore/Ecore_Data.h ${STAGING_INCDIR}/
	install -m 0644 ${S}/ecore.m4 ${STAGING_DATADIR}/aclocal/
	cat ecore-config | sed -e 's,/usr/include,${STAGING_INCDIR},g; \
				      s,/usr/lib,${STAGING_LIBDIR},g; \
				      s,/usr/bin,${STAGING_BINDIR},g; \
				      s,/usr,${STAGING_LIBDIR}/..,g;' > ${STAGING_BINDIR}/ecore-config
	chmod u+x ${STAGING_BINDIR}/ecore-config
}
