DESCRIPTION = "Ecore is the core event abstraction layer and X abstraction layer \
that makes doing selections, Xdnd, general X stuff, and event loops, timeouts \
and idle handlers fast, optimized, and convenient."
HOMEPAGE = "http://www.enlightenment.org"
SECTION = "libs"
PRIORITY = "optional"
PV = "${CVSDATE}"
DEPENDS = "edb eet evas freetype edb-native"

SRC_URI = "cvs://anonymous@cvs.sourceforge.net/cvsroot/enlightenment;module=e17/libs/ecore;date=${CVSDATE}"
S = "${WORKDIR}/ecore"

inherit autotools pkgconfig

export EDB_CONFIG = "${STAGING_BINDIR}/edb-config"
export EET_CONFIG = "${STAGING_BINDIR}/eet-config"
export EVAS_CONFIG = "${STAGING_BINDIR}/evas-config"
export FREETYPE_CONFIG = "${STAGING_BINDIR}/freetype-config"

EXTRA_OECONF = "--enable-ecore-fb --enable-ecore-job \
		--enable-ecore-evas-fb --disable-ecore-evas-x \
		--disable-ecore-evas-gl --enable-ecore-con \
		--enable-ecore-ipc --enable-ecore-txt \
		--disable-ecore-x --enable-ecore-config \
		--disable-openssl"

parts = "Ecore Ecore_Job \
	 Ecore_Txt Ecore_Fb Ecore_Con \
	 Ecore_Ipc Ecore_Evas Ecore_Config"

do_stage () {
	for p in ${parts}; do
		dir=`echo $p|tr A-Z a-z`
		install -m 0644 ${S}/src/lib/$dir/$p.h ${STAGING_INCDIR}/
		oe_libinstall -C src/lib/$dir lib$dir ${STAGING_LIBDIR}/
	done
	install -m 0644 ${S}/src/lib/ecore/Ecore_Data.h ${STAGING_INCDIR}/
	install -m 0644 ${S}/ecore.m4 ${STAGING_DATADIR}/aclocal/
}
