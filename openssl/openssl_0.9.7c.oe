SECTION = "libs"
DESCRIPTION = "Secure Socket Layer (SSL) binary and related cryptographic tools."

SRC_URI = "http://www.openssl.org/source/openssl-${PV}.tar.gz \
	   ${DEBIAN_MIRROR}/main/o/${PN}/${PN}_${PV}-5.diff.gz"
S = "${WORKDIR}/openssl-${PV}"

AR_append = " r"
export CFLAG = "-fPIC -DTHREADS -D_REENTRANT -DDSO_DLFCN -DHAVE_DLFCN_H -DL_ENDIAN -DTERMIO -Wall ${FULL_OPTIMIZATION}"
export DIRS = "crypto ssl"
export EX_LIBS = "-lgcc -ldl -L${STAGING_LIBDIR}"

do_compile () {
	perl util/perlpath.pl ${bindir}
	ln -sf apps/openssl.pod crypto/crypto.pod ssl/ssl.pod doc/
	if test "X${TARGET_OS}" = "Xlinux"; then
		target=${TARGET_OS}-elf
	else
		target=${TARGET_OS}
	fi
	if ! test "X${BUILD_SYS}" = "X${TARGET_SYS}" -o \
		"X${TARGET_ARCH}" = "Xi386" -o \
		"X${TARGET_ARCH}" = "Xi686" -o \
		"X${TARGET_ARCH}" = "Xi586" -o \
		"X${TARGET_ARCH}" = "Xi486"; then
		target="$target-${TARGET_ARCH}"
	fi
	perl ./Configure shared --prefix=${prefix} --openssldir=${libdir}/ssl $target
	oe_runmake -f Makefile.ssl
	perl ./Configure no-shared --prefix=${prefix} --openssldir=${libdir}/ssl $target
	oe_runmake -f Makefile.ssl
}

do_stage () {
	cp --dereference -R include/openssl ${STAGING_INCDIR}/
	oe_libinstall -a -so libcrypto ${STAGING_LIBDIR}
	oe_libinstall -a -so libssl ${STAGING_LIBDIR}
}

do_install () {
	install -d ${D}/${libdir}/pkgconfig
	oe_runmake -f Makefile.ssl INSTALL_PREFIX="${D}" install
	chmod u+rx ${D}/${libdir}/pkgconfig
	chmod 644 ${D}/${libdir}/pkgconfig/openssl.pc
}
