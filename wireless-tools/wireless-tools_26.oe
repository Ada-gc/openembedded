DESCRIPTION = "Tools for the Linux Standard Wireless Extension Subsystem"
SECTION = "base"
PRIORITY = "optional"
DEPENDS = "virtual/kernel"
MAINTAINER = "Michael Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
PR = "r4"

SRC_URI = "http://pcmcia-cs.sourceforge.net/ftp/contrib/wireless_tools.${PV}.tar.gz \
	   file://man.patch;patch=1 \
	   file://wireless-tools.if-pre-up"
S = "${WORKDIR}/wireless_tools.${PV}"

export KERNEL_SOURCE = ${@base_read_file('${STAGING_DIR}/${HOST_SYS}/kernel/kernel-source')}
export KERNEL_VERSION = ${@base_read_file('${STAGING_DIR}/${HOST_SYS}/kernel/kernel-abiversion')}
KERNEL_CCSUFFIX = ${@base_read_file('${STAGING_DIR}/${HOST_SYS}/kernel/kernel-ccsuffix')}
KERNEL_LDSUFFIX = ${@base_read_file('${STAGING_DIR}/${HOST_SYS}/kernel/kernel-ldsuffix')}
KERNEL_CC = "${CCACHE}${HOST_PREFIX}gcc${KERNEL_CCSUFFIX}"
KERNEL_LD = "${LD}${KERNEL_LDSUFFIX}"

do_configure() {
	rm -rf wireless.h
	ln -s ${STAGING_KERNEL_DIR}/include/linux/wireless.h wireless.h
}

do_compile() {
        unset CFLAGS CPPFLAGS CXXFLAGS LDFLAGS
        oe_runmake KERNEL_SRC=${KERNEL_PATH} KERNEL_VERSION=${KERNEL_VERSION} BUILD_SHARED=y
}

do_install() {
	oe_runmake PREFIX=${D} install
	install -d ${D}/etc/network/if-pre-up.d
	install ${WORKDIR}/wireless-tools.if-pre-up ${D}/etc/network/if-pre-up.d/wireless-tools
}

FILES_${PN} = "${bindir} ${sbindir} ${libexecdir} ${libdir}/lib*.so.* \
               ${sysconfdir} ${sharedstatedir} ${localstatedir} \
               /bin /sbin /lib/*.so* ${datadir}/${PN} ${libdir}/${PN} \
	       /etc/network"
