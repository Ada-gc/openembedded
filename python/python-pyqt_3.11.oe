DESCRIPTION = "Python Qt/Embedded + Qt/Palmtop Bindings"
SECTION = "devel/python"
PRIORITY = "optional"
MAINTAINER = "Michael Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
RDEPENDS = "python-core"
DEPENDS = "virtual/libc virtual/libqte2 virtual/libqpe libsip sip-native"
SRCNAME = "pyqt"

SRC_URI = "http://www.vanille.de/mirror/PyQt-x11-gpl-${PV}.tar.gz"

S = "${WORKDIR}/PyQt-x11-gpl-${PV}"

inherit qmake

export QTDIR = "${STAGING_LIBDIR}/.."

EXTRA_QMAKEVARS_POST = " QMAKE_UIC=${STAGING_BINDIR}/uic QMAKE_MOC=${STAGING_BINDIR}/moc QMAKE_RPATH=-Wl,-rpath-link, \
                         CONFIG=qte CONFIG+=warn_on CONFIG+=release \
                         DESTDIR=${STAGING_LIBDIR}/python2.3/site-packages \
                         DEFINES=SIP_MAKE_DLL DEFINES+=SIP_QT_SUPPORT \
                         INCLUDEPATH=. \
                         INCLUDEPATH+=${STAGING_INCDIR}/python2.3 \
                         INCLUDEPATH+=${STAGING_INCDIR} \
                         LIBS=-L${STAGING_LIBDIR}/python2.3/site-packages \
                         LIBS+=-L${STAGING_LIBDIR} LIBS+=-lqte LIBS+=-lqpe LIBS+=-lsip "

PYTHON = "${STAGING_BINDIR}/python"
SIP = "${STAGING_BINDIR}/sip"
QMAKE = "${STAGING_BINDIR}/qmake"
QMAKESPEC = "${QMAKE_MKSPEC_PATH}/qws/${TARGET_OS}-oe-g++"

MODULES = "qt qtcanvas qttable qtpe"

do_configure() {
    for module in ${MODULES}
    do
        mkdir -p ${module}
        ${SIP} -Isip -tWS_QWS -tQtPE_1_6_0 -tQt_2_3_1 -z${FILESDIR}/features -c ${module} -m ${module}.pro.in sip/${module}/${module}mod.sip
        mv -f ${module}.pro.in ${module}/${module}.pro
    done

    for module in ${MODULES}
    do
        cd ${S}/${module}
        ${QMAKE} -spec ${QMAKESPEC} -after TARGET=${module}cmodule ${EXTRA_QMAKEVARS_POST}
    done
}

do_compile() {
    for module in ${MODULES}
    do
        cd ${S}/${module}
        oe_runmake
    done
}

do_stage() {
    install -d ${STAGING_SIPDIR}
    for module in ${MODULES}
    do
        cp -a ${S}/sip/${module}/*.sip ${STAGING_SIPDIR}/
    done
}

do_install() {
    install -d ${D}/${libdir}/python2.3/site-packages/
    oe_soinstall ${STAGING_LIBDIR}/python2.3/site-packages/libsip.so.11.0.1 ${D}/${libdir}/python2.3/site-packages/
    for module in ${MODULES}
    do
        oe_soinstall ${STAGING_LIBDIR}/python2.3/site-packages/lib${module}cmodule.so.1.0.0 ${D}/${libdir}/python2.3/site-packages/
        install -m 0644 ${module}/${module}.py ${D}/${libdir}/python2.3/site-packages/
    done
}

FILES_${PN} = "/usr/lib/python2.3/site-packages"
