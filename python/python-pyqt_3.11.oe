DESCRIPTION = "Python Qt/Embedded + Qt/Palmtop Bindings"
HOMEPAGE = "http://www.vanille.de/projects/python.spy"
SECTION = "devel/python"
PRIORITY = "optional"
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
RDEPENDS = "python-core"
DEPENDS = "virtual/libqte2 virtual/libqpe"
SRCNAME = "pyqt"
PR = "2"

SRC_URI = "http://www.vanille.de/mirror/PyQt-x11-gpl-${PV}.tar.gz \
           file://features"
S = "${WORKDIR}/PyQt-x11-gpl-${PV}"

inherit qmake sip

QMAKE_PROFILES = "pyqt.pro"
EXTRA_SIPTAGS = "-tWS_QWS -tQtPE_1_6_0 -tQt_2_3_1"
SIP_MODULES = "qt qtcanvas qttable qtpe"
SIP_FEATURES = "${WORKDIR}/features"
EXTRA_OEMAKE = " MAKEFLAGS= "

EXTRA_QMAKEVARS_POST = " QMAKE_UIC=${STAGING_BINDIR}/uic QMAKE_MOC=${STAGING_BINDIR}/moc QMAKE_RPATH=-Wl,-rpath-link, \
                         CONFIG=qte CONFIG+=warn_on CONFIG+=release \
                         TARGET=cmodule DESTDIR= VERSION=1.0.0 \
                         DEFINES=SIP_MAKE_DLL DEFINES+=SIP_QT_SUPPORT DEFINES+=QWS \
                         INCLUDEPATH=. \
                         INCLUDEPATH+=${STAGING_INCDIR}/python2.3 \
                         INCLUDEPATH+=${STAGING_INCDIR} \
                         LIBS=-L${STAGING_LIBDIR}/python2.3/site-packages \
                         LIBS+=-L${STAGING_LIBDIR} LIBS+=-lqte LIBS+=-lqpe LIBS+=-lsip "

do_configure_prepend() {
    echo -e "TEMPLATE=subdirs\nSUBDIRS=qt qtcanvas qttable qtpe\n" >pyqt.pro
}

do_stage() {
    install -d ${STAGING_SIPDIR}/qt/
    for module in ${SIP_MODULES}
    do
        cp -a ${S}/sip/${module}/*.sip ${STAGING_SIPDIR}/qt/
	install -m 0755 ${module}/libcmodule.so ${STAGING_LIBDIR}/python2.3/site-packages/lib${module}cmodule.so
    done
}

do_install() {
    install -d ${D}/${libdir}/python2.3/site-packages/
    oe_libinstall -so -C ${STAGING_LIBDIR}/python2.3/site-packages libsip ${D}/${libdir}/python2.3/site-packages
    for module in ${SIP_MODULES}
    do
	install -m 0755 ${module}/libcmodule.so ${D}/${libdir}/python2.3/site-packages/lib${module}cmodule.so
        install -m 0644 ${module}/${module}.py ${D}/${libdir}/python2.3/site-packages/
    done
}

FILES_${PN} = "/usr/lib/python2.3/site-packages"
