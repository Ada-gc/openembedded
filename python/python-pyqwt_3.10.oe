DESCRIPTION = "Python QWT Bindings"
SECTION = "devel/python"
PRIORITY = "optional"
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
RDEPENDS = "python-core python-pyqt (${PV}) python-numeric qwt"
DEPENDS = "virtual/libqte2 python-numeric python-pyqt qwt"
SRCNAME = "pyqwt"

SRC_URI = "http://pyqwt.sourceforge.net/snapshot/PyQwt-20040118.tar.gz \
           file://qt2.x-compat.patch;patch=1 \
           file://qtmod-sip-staging.patch;patch=1 \
           file://features"
S = "${WORKDIR}/PyQwt-20040118"

inherit qmake sip

QMAKE_PROFILES = "pyqwt.pro"
EXTRA_SIPTAGS = "-tWS_QWS -tQtPE_1_6_0 -tQt_2_3_1"
SIP_MODULES = "qwt"
SIP_FEATURES = "${WORKDIR}/features"   
EXTRA_OEMAKE = " MAKEFLAGS= "

EXTRA_QMAKEVARS_POST = " QMAKE_UIC=${STAGING_BINDIR}/uic QMAKE_MOC=${STAGING_BINDIR}/moc QMAKE_RPATH=-Wl,-rpath-link, \
                         CONFIG=qte CONFIG+=warn_on CONFIG+=release \
                         TARGET=cmodule DESTDIR= VERSION=1.0.0 \
                         DEFINES=SIP_MAKE_DLL DEFINES+=SIP_QT_SUPPORT DEFINES+=HAS_NUMERIC DEFINES+=QWS \
                         INCLUDEPATH=. INCLUDEPATH+=../numpy \
                         INCLUDEPATH+=${STAGING_INCDIR}/python2.3 \
                         INCLUDEPATH+=${STAGING_INCDIR}/ \
                         LIBS=-L${STAGING_LIBDIR}/python2.3/site-packages \
                         LIBS+=-L${STAGING_LIBDIR} LIBS+=-lqte LIBS+=-lqpe LIBS+=-lsip "

do_generate_prepend() {
    echo -e "TEMPLATE=subdirs\nSUBDIRS=qwt\n" >pyqwt.pro
    
    echo "%Makefile qwt.pro.in" >>sip/qwtmod.sip
    echo "TEMPLATE=lib" >>sip/qwtmod.sip
    echo 'SOURCES = $B' >>sip/qwtmod.sip
    echo 'HEADERS = $H' >>sip/qwtmod.sip
    echo "%End" >>sip/qwtmod.sip

    mkdir sip/qwt/
    mv sip/*.* sip/qwt/
}

do_stage() {
    install -d ${STAGING_SIPDIR}
    for module in ${SIP_MODULES}
    do
        cp -a ${S}/sip/${module}/*.sip ${STAGING_SIPDIR}/
        install -m 0755 ${module}/libcmodule.so ${STAGING_LIBDIR}/python2.3/site-packages/lib${module}cmodule.so
    done
}

do_install() {
    install -d ${D}/${libdir}/python2.3/site-packages/
    oe_libinstall -so -C ${STAGING_LIBDIR}/python2.3/site-packages libsip ${D}/${libdir}/python2.3/site-packages
    for module in ${SIP_MODULES}
    do
        install -m 0755 ${module}/libcmodule.so ${D}/${libdir}/python2.3/site-packages/lib${module}cmodule.so
#        install -m 0644 ${module}/${module}.py ${D}/${libdir}/python2.3/site-packages/
    done

    install -d ${D}/${libdir}/python2.3/site-packages/qwt
    install -m 0644 ${S}/qwt/*.py ${D}/${libdir}/python2.3/site-packages/qwt/
}

FILES_${PN} = "/usr/lib/python2.3/site-packages"
