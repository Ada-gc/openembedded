DESCRIPTION = "Base library for the Qt Palmtop Environment"
SECTION = "base"
PRIORITY = "optional"
MAINTAINER = "Michael Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
DEPENDS = "virtual/libc uicmoc-native qte-for-opie"
PROVIDES = "virtual/libqpe"
PV = "1.1.4-cvs-${CVSDATE}"

SRC_URI = "cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/library;date=${CVSDATE}"
S = "${WORKDIR}/library"

inherit qmake

QMAKE_PROFILES = "library.pro"

export QTDIR = "${STAGING_LIBDIR}/.."
export OPIEDIR = "${STAGING_LIBDIR}/.."

CXXFLAGS_append = " -DQWS -fno-rtti -fno-exceptions"

#
# for developing - qmake.oeclass should gather this automatically
#
export QMAKESPEC = "${QMAKE_MKSPEC_PATH}/qws/linux-sharp-g++"

do_compile() {
        mkdir -p ${STAGING_INCDIR}/qpe \
                 ${STAGING_INCDIR}/qtopia \
                 ${STAGING_INCDIR}/opie \
                 ${STAGING_INCDIR}/opie2 \
                 ${STAGING_INCDIR}/qtopia/private
	for i in ${S}/*.h
	do
		install -m 0644 $i ${STAGING_INCDIR}/qpe/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/private/`basename $i`
	done
	for i in ${S}/backend/*.h
	do
		install -m 0644 $i ${STAGING_INCDIR}/qpe/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/private/`basename $i`
	done
	rm -f ${STAGING_INCDIR}/qtopia/*_p.h # remove private stuff out of qtopia
	oe_runmake MOC=${STAGING_BINDIR}/moc UIC=${STAGING_BINDIR}/uic DESTDIR=${S}
}

do_stage() {
	[ -e ${STAGING_LIBDIR}/libqpe.so.1.5.0 ] || oe_soinstall libqpe.so.1.5.0 ${STAGING_LIBDIR}
	# include files already done during do_compile
}

do_install() {
	install -d ${D}${palmtopdir}/lib
	oe_soinstall ${STAGING_LIBDIR}/libqpe.so.1.5.0 ${D}${palmtopdir}/lib
}

PACKAGES = "libqpe1"
FILES_libqpe1 = "${palmtopdir}/lib"
