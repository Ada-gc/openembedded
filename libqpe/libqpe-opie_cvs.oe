DESCRIPTION = "Base library for the Qt Palmtop Environment"
SECTION = "opie/libs"
PRIORITY = "optional"
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
DEPENDS = "uicmoc-native qte-for-opie"
PROVIDES = "virtual/libqpe"
PV = "1.1.6-cvs-${CVSDATE}"
PR = "r1"

SRC_URI = "cvs://anoncvs:anoncvs@cvs.handhelds.org/cvs;module=opie/library \
	   file://beagle-debugging.patch;patch=1 "
S = "${WORKDIR}/library"

inherit qmake

QMAKE_PROFILES = "library.pro"

export QTDIR = "${STAGING_LIBDIR}/.."
export OPIEDIR = "${STAGING_LIBDIR}/.."
EXTRA_QMAKEVARS_PRE  = "CONFIG+=LIBQPE_WITHROHFEEDBACK"
EXTRA_QMAKEVARS_POST = "DESTDIR="

CXXFLAGS_append = " -DQWS -DOPIE_NO_ERASE_RECT_HACKFIX -DOPIE_NEW_MALLOC  -fno-rtti -fno-exceptions"

do_compile() {
        mkdir -p ${STAGING_INCDIR}/qpe \
                 ${STAGING_INCDIR}/qtopia \
                 ${STAGING_INCDIR}/opie \
                 ${STAGING_INCDIR}/opie2 \
                 ${STAGING_INCDIR}/qtopia/private
	for i in ${S}/*.h
	do
		install -m 0644 $i ${STAGING_INCDIR}/qpe/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/private/`basename $i`
	done
	for i in ${S}/backend/*.h
	do
		install -m 0644 $i ${STAGING_INCDIR}/qpe/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/`basename $i`
		install -m 0644 $i ${STAGING_INCDIR}/qtopia/private/`basename $i`
	done
	rm -f ${STAGING_INCDIR}/qtopia/*_p.h # remove private stuff out of qtopia
	oe_runmake MOC=${STAGING_BINDIR}/moc UIC=${STAGING_BINDIR}/uic
}

do_stage() {
	oe_libinstall -so libqpe ${STAGING_LIBDIR}
	# include files already done during do_compile
}

do_install() {
	oe_libinstall -so libqpe ${D}/${palmtopdir}/lib
}

PACKAGES = "libqpe1"
FILES_libqpe1 = "${palmtopdir}/lib"
