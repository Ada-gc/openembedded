DESCRIPTION = "User space helper for WPA and WPA2 client operations. \
The Supplicant is used in WPA/WPA2 key handshakes to authenticate \
with the AP and to generate dynamic encryption keys (TKIP or CCMP)."
SECTION = "kernel/userland"
PRIORITY = "optional"
MAINTAINER = "Michael 'Mickey' Lauer <mickey@Vanille.de>"
LICENSE = "GPL"
DEPENDS = "openssl"
RDEPENDS = "hostap-modules (${PV}) hostap-utils (${PV}) hostap-daemon (${PV})"
PR = "r1"

SRC_URI = "http://hostap.epitest.fi/releases/wpa_supplicant-${PV}.tar.gz \
	   file://wpa-defconfig \
	   file://wpa_supplicant_default.conf"

S = "${WORKDIR}/wpa_supplicant-${PV}"

EXTRA_OEMAKE="LIBS='-L${STAGING_LIBDIR} -lssl -lcrypto' LIBS_p='-L${STAGING_LIBDIR} -lssl -lcrypto'"

do_configure() {
	install -m 0644 ${WORKDIR}/wpa-defconfig .config
}

do_install() {
	install -d ${D}/${sysconfdir} ${D}/${bindir}
	install -m 0644 wpa_supplicant.conf ${D}/${sysconfdir}/wpa_supplicant.conf.sample
	install -m 0644 ../wpa_supplicant_default.conf ${D}/${sysconfdir}/wpa_supplicant.conf
	install -m 0755 wpa_passphrase ${D}/${bindir}
	install -m 0755 wpa_cli        ${D}/${bindir}
	install -m 0755 wpa_supplicant ${D}/${bindir}
}

