#-----------------------------------------------------------------------------
#@TYPE: Distribution
#@NAME: SHR
#@DESCRIPTION: Linux Distribution Configuration for Mobile Phones
#-----------------------------------------------------------------------------

# set default locale for images - we have to do that before
# including angstrom-2007-for-openmoko.inc because otherwise
# it will be already set to en-gb
IMAGE_LINGUAS ?= "en-us"

# Set the toolchain type (internal, external) and brand (generic, csl etc.)
TOOLCHAIN_TYPE ?= "internal"
TOOLCHAIN_BRAND ?= ""

require conf/distro/include/preferred-shr-versions.inc
require conf/distro/include/angstrom-2007-for-openmoko.inc

#
# Header
#
DISTRO_NAME = "shr"
DISTRO_VERSION = "-${DATE}"
#DISTRO_TYPE = "release"
DISTRO_TYPE = "debug"
DISTRO = "shr"

OVERRIDES = "local:${MACHINE}:shr:angstrom:${TARGET_OS}:${TARGET_ARCH}:build-${BUILD_OS}:fail-fast:pn-${PN}"

INHERIT += "packaged-staging"

CVS_TARBALL_STASH += "http://build.shr-project.org/sources/"

PREMIRRORS = "(ftp|https?)$://.*/.* http://build.shr-project.org/sources/"

DISTRO_FEED_PREFIX ?= "shr-dev"
DISTRO_FEED_URI ?= "http://build.shr-project.org/shr-testing/ipk"

AB_DEPLOY_EXTRA ?= ""
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE_ARCH}/${AB_DEPLOY_EXTRA}"

IMAGE_INITSCRIPTS = "initscripts-shr"
DISTRO_INITSCRIPTS = "initscripts-shr"
DISTRO_DEV_MANAGER = "udev"

IPKG_VARIANT = "opkg"

# We try to minimize the distro features in the default image.
DISTRO_FEATURES = "wifi ppp alsa ext2 vfat usbhost"

# Some features (bluetooth, usbgadget) include packages we do not like.
# Pick some of the packages manually.
DISTRO_EXTRA_RDEPENDS += " \
    bluez4 \
    "
DISTRO_EXTRA_RDEPENDS_append_om-gta02 = " \
    kernel-module-g-ether \
    "

DISTRO_BLUETOOTH_MANAGER = "bluez4 blueprobe blueman"

# 2.18 seems to break badly on 64-bit systems
PREFERRED_VERSION_binutils ?= "2.17.50.0.12"
PREFERRED_VERSION_binutils-cross ?= "2.17.50.0.12"

# Same kernel so same usb
PACKAGE_ARCH_pn-usb-gadget-mode_om-gta01 = "armv4t"
PACKAGE_ARCH_pn-usb-gadget-mode_om-gta02 = "armv4t"
# same kernel so same keymaps
PACKAGE_ARCH_pn-keymaps_om-gta01 = "armv4t"
PACKAGE_ARCH_pn-keymaps_om-gta02 = "armv4t"
# neod knows about both machines
PACKAGE_ARCH_pn-neod_om-gta01 = "armv4t"
PACKAGE_ARCH_pn-neod_om-gta02 = "armv4t"

# Select package providers
PREFERRED_PROVIDER_virtual/gail = "gtk+"
PREFERRED_PROVIDER_gtk+ = "gtk+"
PREFERRED_PROVIDER_opkg = "opkg"
PREFERRED_PROVIDER_libgsmd = "libgsmd"
PREFERRED_PROVIDER_bluez-libs = "bluez4"
PREFERRED_PROVIDER_bluez-utils-dbus = "bluez4"
PREFERRED_PROVIDER_bluez-utils = "bluez4"

BBMASK = " bluez-libs bluez-utils "

PREFERRED_PROVIDER_virtual/libusb0 = "libusb"
PREFERRED_PROVIDER_libfribidi = "libfribidi"
PREFERRED_PROVIDER_util-linux = "util-linux-ng"
# for jalimo
PREFERRED_PROVIDER_virtual/javac-native = "ecj-bootstrap-native"
PREFERRED_PROVIDER_classpath = "classpath"
PREFERRED_PROVIDER_virtual/java-initial = "cacao-initial"
PREFERRED_PROVIDER_swt3.4-gtk = "swt3.4-gtk"
PREFERRED_PROVIDER_midpath = "midpath-openmoko"
PREFERRED_PROVIDER_virtual/java-native = "jamvm-native"
# We don't use this, but select one to shut bitbake up.
PREFERRED_PROVIDER_virtual/libqte2 = "qte"

PREFERRED_PROVIDER_shr-splash-theme-simple = "shr-splash-theme-simple"
PREFERRED_PROVIDER_virtual/shr-splash-theme = "shr-splash-theme-simple"
PREFERRED_PROVIDER_frameworkd-config-shr = "frameworkd-config-shr"
PREFERRED_PROVIDER_frameworkd-config = "frameworkd-config-shr"
PREFERRED_PROVIDER_frameworkd = "frameworkd"
PREFERRED_PROVIDER_gpsd = "fso-gpsd"
PREFERRED_PROVIDER_e-wm-menu = "e-wm-menu-shr"
PREFERRED_PROVIDER_openmoko-alsa-scenarios = "alsa-scenarii-shr"
PREFERRED_PROVIDER_virtual/alsa-scenarios = "alsa-scenarii-shr"

# Helper to say what image we built
SHR_VERSION_FILE = "${IMAGE_ROOTFS}/${sysconfdir}/shr-version"
ROOTFS_POSTPROCESS_COMMAND += "OLD_PWD=$PWD; cd `dirname '${FILE_DIRNAME}'`; echo Tag Name: `git tag|tail -n 1`> ${SHR_VERSION_FILE};cd $OLD_PWD;"
ROOTFS_POSTPROCESS_COMMAND += "OLD_PWD=$PWD; cd `dirname '${FILE_DIRNAME}'`; echo VERSION: `git-log -n1 --pretty=oneline|awk '{print $1}'` >> ${SHR_VERSION_FILE}; cd $OLD_PWD;"
ROOTFS_POSTPROCESS_COMMAND += "OLD_PWD=$PWD; cd `dirname '${FILE_DIRNAME}'`; echo Branch: ` git branch |awk '/*/{print $2}'` >> ${SHR_VERSION_FILE}; cd $OLD_PWD;"
ROOTFS_POSTPROCESS_COMMAND += "echo Build Host: `cat /etc/hostname` >> ${SHR_VERSION_FILE};"
ROOTFS_POSTPROCESS_COMMAND += "echo Time Stamp: `date -R` >> ${SHR_VERSION_FILE};"

SRCPV = "${@bb.fetch.get_srcrev(d)}"

#Package customization
EXTRA_OECONF-pn_qemu += " --target-list=arm-linux-user,arm-softmmu"
